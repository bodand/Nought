plugins {
    id 'java'
    id 'org.asciidoctor.jvm.convert' version '4.0.3'
    id 'org.asciidoctor.jvm.gems' version '4.0.3'
    id 'org.asciidoctor.jvm.pdf' version '4.0.3'
}

group 'hu.kszi2'
version '1.0.0'

repositories {
    mavenCentral()
    ruby {
        gems()
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'

    // tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'

    testImplementation 'org.mockito:mockito-junit-jupiter:4.9.0'

    // Docs
    asciidoctorGems 'rubygems:rouge:3.30.0'
}

test {
    useJUnitPlatform()
}

pdfThemes {
    local 'plex.yml', {
        themeDir = file('docs')
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'hu.kszi2.nought.gui.Nought'
    }
}

asciidoctorj {
    modules {
        diagram.use()
        diagram.version '2.2.3'
    }
}

tasks.asciidoctorPdf {
    dependsOn asciidoctorGemsPrepare

    asciidoctorj {
        requires 'rouge'
    }

    fontsDirs += file('docs/fonts')
    theme 'plex.yml'
    sourceDir file('docs')
    outputDir file('build/docs')
}